apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 0
def versionBuild = project.hasProperty('buildNumber') ? buildNumber.toInteger() : 1

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "USER_API_URL", "\"https://user.gini.net\""
        buildConfigField "String", "SWITCH_API_URL", "\"https://switch-api.gini.net\""

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:exifinterface:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibraryVersion"

    compile "com.squareup.okhttp3:okhttp:$rootProject.ext.okhttpVersion"

    testCompile "junit:junit:$rootProject.ext.junitVersion"
    testCompile "com.squareup.okhttp3:mockwebserver:$rootProject.ext.okhttpVersion"
    testCompile 'org.json:json:20140107'
    testCompile 'net.jodah:concurrentunit:0.4.2'
    testCompile "org.mockito:mockito-core:2.7.19"

    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.0'
    androidTestCompile 'com.android.support.test:runner:1.0.0'

    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2';
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2';
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
}

task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator)) + configurations.compile
    failOnError false
}

publish {
    artifactId = 'switch'
    userOrg = 'gini'
    groupId = 'net.gini'
    publishVersion = '0.9.0'
    description = "The Gini Switch SDK allows to capture & analyze documents (e.g. electricity bills). The information extracted can then be used to easily switch to a new contract or tariff."
    website = 'https://github.com/gini/gini-switch-sdk-android'
}
