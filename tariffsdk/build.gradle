apply plugin: 'com.android.library'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 0
def versionBuild = project.hasProperty('buildNumber') ? buildNumber.toInteger() : 1

def clientId = project.hasProperty('clientId') ? clientId.toString() : "clientId"
def clientSecret = project.hasProperty('clientSecret') ? clientSecret.toString() : "clientSecret"

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "USER_API_URL", "\"https://user.stage.gini.net\""
        buildConfigField "String", "TARIFF_API_URL", "\"https://switch-api.stage.gini.net\""
        buildConfigField "String", "CLIENT_ID", "\"${clientId}\""
        buildConfigField "String", "CLIENT_SECRET", "\"${clientSecret}\""

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:exifinterface:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibraryVersion"

    compile "com.squareup.okhttp3:okhttp:$rootProject.ext.okhttpVersion"

    testCompile "junit:junit:$rootProject.ext.junitVersion"
    testCompile "com.squareup.okhttp3:mockwebserver:$rootProject.ext.okhttpVersion"
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'org.json:json:20140107'
    testCompile 'net.jodah:concurrentunit:0.4.2'
    testCompile 'org.mockito:mockito-core:2.7.19'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
}

task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator)) + configurations.compile
    failOnError false
}
